
(* Odig toplevel bootstrap. *)

(* First we need this phrase to see Toploop's API which allows us to
   detect utop. N.B. in ocaml we only have Topdirs and Toploop the
   rest is expunged. *)
let () =
  Topdirs.dir_directory "+compiler-libs"
;;

(* This phrase allows us to use Odig in the next one. It would be nice to be
   able to expunge this id from the environment but we need to share it for the
   next phrase. *)
let __odig_deps =
  let deps = (* list of (pkg, cmas, toplevels in which to load). *)
    let all = [ `Utop; `OCaml ] in
    [ "ocaml", ["unix.cma"], [ `OCaml ];

      (* opam-format deps *)
      "opam-file-format", ["opam-file-format.cma"], all;
      "uutf", ["uutf.cma"], all;
      "jsonm", ["jsonm.cma"], all;
      "re", ["re.cma"; "re_emacs.cma"; "re_str.cma"], all;
      "ocamlgraph", ["graph.cma"], all;
      "ocaml", ["bigarray.cma"], all;
      "opam-core", ["opam-core.cma"], all;

      (* odig deps and odig *)
      "opam-format", ["opam-format.cma"], all;
      "asetmap", ["asetmap.cma"], all;
      "rresult", ["rresult.cma"], all;
      "astring", ["astring.cma"], all;
      "fpath", ["fpath.cma"], all;
      "fmt", ["fmt.cma"; "fmt_tty.cma"], all;
      "logs", ["logs.cma"; "logs_fmt.cma"], all;
      "bos", ["bos.cma"; "bos_setup.cma"], all;
      "mtime", ["mtime.cma"], all;
      "mtime/os", ["mtime_clock.cma"], all;
      "ocaml", ["ocamlcommon.cma"], [`OCaml];
      "odig", ["odig.cma"], all; ]
  in
  let load_dep which (pkg, cmas, tops) = match List.mem which tops with
  | false -> ()
  | true ->
      if pkg <> "ocaml" then Topdirs.dir_directory ("%%LIBDIR%%/" ^ pkg);
      List.iter (Topdirs.dir_load Format.err_formatter) cmas
  in
  let which = match Hashtbl.mem Toploop.directive_table "utop_help" with
  | false -> `OCaml
  | true -> `Utop
  in
  List.iter (load_dep which) deps;
  deps
;;

let () =
  (* We can now announce Odig and make it aware of what we already loaded
     in the toplevel. *)
  let assume_dep (pkg, cmas, _) =
    let dir = Fpath.v ("%%LIBDIR%%/" ^ pkg) in
    let cma_path cma = Fpath.(dir / cma) in
    if pkg <> "ocaml" then Odig.Private.Top.assume_inc dir;
    List.iter (fun cma -> Odig.Private.Top.assume_obj (cma_path cma)) cmas
  in
  List.iter assume_dep __odig_deps;
  Odig.Private.Top.announce ()
;;
